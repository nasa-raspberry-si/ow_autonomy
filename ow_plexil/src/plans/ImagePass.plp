// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Auxilliary plan for TakePanorama.  Performs a single pass, panning the camera
// with specified increment, direction, and range.  Tilt is unchanged.

Command log_debug (...);
LibraryAction TakePicture ();
LibraryAction PanAndShoot (In Real PanAngle);

Command set_checkpoint(String,Boolean,String);
String Lookup ToString(...);

ImagePass: UncheckedSequence
{
  In Real PanLo, PanHi, PanIncrement, Tilt; // Tilt just used for checkpoints
  In String CheckpointName;
  InOut Real PanAngle;
  InOut Boolean ReversePan;

  LibraryCall TakePicture();
  if (ReversePan) {
    log_debug ("Reverse pan. Pan =", PanAngle, "Lo=", PanLo,
               "Increment=", PanIncrement);
    while (PanAngle - PanIncrement > PanLo) {
      PanAngle = PanAngle - PanIncrement;
      LibraryCall PanAndShoot (PanAngle = PanAngle);
      set_checkpoint(CheckpointName,
                     true,
                     Lookup(ToString(ReversePan,"_",Tilt,"_",PanAngle)));
    }
    if (PanAngle > PanLo) {
      PanAngle = PanLo;
      LibraryCall PanAndShoot (PanAngle = PanAngle);
      set_checkpoint(CheckpointName,
                     true,
                     Lookup(ToString(ReversePan,"_",Tilt,"_",PanAngle)));
    }
    endif
  }
  else {
    log_debug ("Forward pan. Pan =", PanAngle, "Hi=", PanHi,
               "Increment=", PanIncrement);
    while (PanAngle + PanIncrement < PanHi) {
      PanAngle = PanAngle + PanIncrement;
      LibraryCall PanAndShoot (PanAngle = PanAngle);
      set_checkpoint(CheckpointName,
                     true,
                     Lookup(ToString(ReversePan,"_",Tilt,"_",PanAngle)));
    }
    if (PanAngle < PanHi) {
      PanAngle = PanHi;
      LibraryCall PanAndShoot (PanAngle = PanAngle);
      set_checkpoint(CheckpointName,
                     true,
                     Lookup(ToString(ReversePan,"_",Tilt,"_",PanAngle)));
    }
    endif
  }
  endif
  ReversePan = !ReversePan;
}
