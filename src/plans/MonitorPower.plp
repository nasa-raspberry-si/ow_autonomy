// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Check and report battery values at regular interval and set health variables
// accordingly.

#include "plan-interface.h"

MonitorPower:
{
  In Boolean continue;
  InOut Boolean battery_temp_ok, battery_life_ok, battery_charge_ok, all_ok;

  // Thresholds (made up for now)
  Real LowCharge = 0.10; // percentage
  Real HighTemp  = 30;   // celsius

  Integer Iteration = 0;

  Exit !continue;

  Iterate:
  {
    Repeat true;

    Iteration = Iteration + 1;

    battery_charge_ok = Lookup(StateOfCharge) >= LowCharge;
    battery_temp_ok   = Lookup(BatteryTemperature) < HighTemp;

    // Report levels every 3 iterations, or if there's a problem, to reduce
    // output clutter.
    if (Iteration % 3 == 0 || !battery_charge_ok || !battery_temp_ok) {
      log_info ("Battery: state of charge: ", Lookup(StateOfCharge));
      log_info ("Battery: remaining useful life: ", Lookup(RemainingUsefulLife));
      log_info ("Battery: temperature: ", Lookup(BatteryTemperature));
      if (!battery_charge_ok) log_warning ("Battery charge is low!");
      if (!battery_temp_ok) log_warning ("Battery too hot!");
    }
    
    all_ok = battery_charge_ok && battery_temp_ok;

    Wait 30; // seconds, arbitrary choice
  }
}
